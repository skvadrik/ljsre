%{

#include <stddef.h> // NULL

#include "debug_parser.h"
#include "parser.h"

RE * Parser::parse (TokenArray & tok_arr, REArray & re_arr, slab_allocator<> & allocator)
{
    TokenValue semantics;

#   define LALR2C_R_TOKEN_TYPE             tok_arr.start[tok_arr.index].type
#   define LALR2C_R_TOKEN_SEMANTICS        tok_arr.start[tok_arr.index].value

#   define LALR2C_RW_STACK_STATE(n)        (stack_top - n)->state
#   define LALR2C_RW_STACK_SEMANTICS(n)    (stack_top - n)->semantics

#   define LALR2C_E_SHIFT_TOKEN()          do { ++ tok_arr.index; } while (0)
#   define LALR2C_E_RESERVE_STACK()        do { ++ stack_top; } while (0)
#   define LALR2C_E_POP_STACK(n)           do { stack_top -= n; } while (0)

#   define LALR2C_RW_TMP_SEMANTICS         semantics

#   define LALR2C_RW_IS_1ST_CALL           is_start

%}

%token    T_ALT
%token    T_ASSERT
%token    T_ASSERT_FOLLOW
%token    T_ASSERT_FOLLOW_NOT
%token    T_BACKREF
%token    T_CLASS
%token    T_CLASS_END
%token    T_CLASS_START
%token    T_CLASS_START_NEG
%token    T_CLASS_UNBOXED
%token    T_COMMA
%token    T_DASH
%token    T_DOT
%token    T_LBRACE_CAPTURE
%token    T_LBRACE_FOLLOW
%token    T_LBRACE_FOLLOW_NOT
%token    T_LBRACE_LAZY
%token    T_NUMBER
%token    T_ONE_MANY
%token    T_RBRACE
%token    T_RUNE
%token    T_RUNE_UNBOXED
%token    T_TIMES
%token    T_TIMES_FROM
%token    T_TIMES_FROM_TO
%token    T_ZERO_MANY
%token    T_ZERO_ONE

%fail        { return NULL; }

%start       pattern
%stop        T_LAMBDA

%%

class_ranges
: {
    $$.rune_vector = allocator.allocate_type<RuneVector> ();
    new ($$.rune_vector) RuneVector (allocator);
}
| class_ranges T_RUNE_UNBOXED {
    $1.rune_vector->push_back ($2.rune);
    $$ = $1;
}
| class_ranges T_CLASS_UNBOXED {
    for (unsigned int i = 0; i < $2.rune_vector->size (); ++i)
        $1.rune_vector->push_back ((* $2.rune_vector)[i]);
    $$ = $1;
}
| class_ranges T_RUNE_UNBOXED T_DASH T_RUNE_UNBOXED {
    const Rune r1 = $2.rune;
    const Rune r2 = $4.rune;
    const Rune r = r2 << 16 | r1;
    $1.rune_vector->push_back (r);
    $$ = $1;
}
;

class_body
: class_ranges    /* empty */
| class_ranges T_DASH {
    const Rune r = '-';
    $1.rune_vector->push_back (r);
    $$ = $1;
}
| T_DASH class_ranges {
    const Rune r = '-';
    $1.rune_vector->push_back (r);
    $$ = $1;
}
| T_DASH class_ranges T_DASH {
    const Rune r = '-';
    $1.rune_vector->push_back (r);
    $$ = $1;
}
;

class
: T_CLASS_START class_body T_CLASS_END {
    $$.re = savere_rune_class (re_arr, $2.rune_vector);
}
| T_CLASS_START_NEG class_body T_CLASS_END {
    $$.re = savere_rune_class_neg (re_arr, $2.rune_vector);
}
;

atom
: T_RUNE    /* empty */
| T_BACKREF    /* empty */
| T_DOT    /* empty */
| class    /* empty */
| T_CLASS    /* empty */
| T_LBRACE_CAPTURE disjunction T_RBRACE {
    $$.re = savere_capture (re_arr, $2.re);
}
| T_LBRACE_LAZY disjunction T_RBRACE {
    $$ = $2;
}
;

assertion
: T_ASSERT    /* empty */
| T_LBRACE_FOLLOW disjunction T_RBRACE {
    $$.re = savere_assert_follow (re_arr, $2.re);
}
| T_LBRACE_FOLLOW_NOT disjunction T_RBRACE {
    $$.re = savere_assert_follow_not (re_arr, $2.re);
}
;

term
: assertion    /* empty */
| atom    /* empty */

/* "e*|e*?" */
| atom T_ZERO_MANY {
    $$.re = savere_zero_many (re_arr, $1.re);
}
| atom T_ZERO_MANY T_ZERO_ONE {
    $$.re = savere_zero_many_lazy (re_arr, $1.re);
}

/* "e+"/"e+?" */
| atom T_ONE_MANY {
    $$.re = savere_one_many (re_arr, $1.re);
}
| atom T_ONE_MANY T_ZERO_ONE {
    $$.re = savere_one_many_lazy (re_arr, $1.re);
}

/* "e?"/"e??" */
| atom T_ZERO_ONE {
    $$.re = savere_zero_one (re_arr, $1.re);
}
| atom T_ZERO_ONE T_ZERO_ONE {
    $$.re = savere_zero_one_lazy (re_arr, $1.re);
}

/* "e{n}"/"e{n}?" */
| atom T_NUMBER {
    $$.re = savere_times_exact (re_arr, $1.re, $2.number);
}
| atom T_NUMBER T_ZERO_ONE {
    $$.re = savere_times_exact_lazy (re_arr, $1.re, $2.number);
}

/* "e{n,}"/"e{n,}?" */
| atom T_NUMBER T_COMMA {
    $$.re = savere_times_lower (re_arr, $1.re, $2.number);
}
| atom T_NUMBER T_COMMA T_ZERO_ONE {
    $$.re = savere_times_lower_lazy (re_arr, $1.re, $2.number);
}

/* "e{n,m}"/"e{n,m}?" */
| atom T_NUMBER T_COMMA T_NUMBER {
    $$.re = savere_times_lower_upper (re_arr, $1.re, $2.number, $3.number);
}
| atom T_NUMBER T_COMMA T_NUMBER T_ZERO_ONE {
    $$.re = savere_times_lower_upper_lazy (re_arr, $1.re, $2.number, $3.number);
}
;

alternative
: term    /* empty */
| alternative term {
    $$.re = savere_cat (re_arr, $1.re, $2.re);
}
;

disjunction
: /* empty */ {
    $$.re = savere_empty (re_arr);
}
| alternative    /* empty */
| alternative T_ALT disjunction {
    $$.re = savere_alt (re_arr, $1.re, $3.re);
}
| T_ALT disjunction {
    RE * p_empty = savere_empty (re_arr);
    $$.re = savere_alt (re_arr, p_empty, $2.re);
}
;

pattern
: disjunction {
    return $1.re;
}
;


%{
    ;

#   undef LALR2C_R_TOKEN_TYPE
#   undef LALR2C_R_TOKEN_SEMANTICS

#   undef LALR2C_RW_STACK_STATE
#   undef LALR2C_RW_STACK_SEMANTICS

#   undef LALR2C_E_SHIFT_TOKEN
#   undef LALR2C_E_RESERVE_STACK
#   undef LALR2C_E_POP_STACK

#   undef LALR2C_RW_TMP_SEMANTICS

#   undef LALR2C_RW_IS_1ST_CALL

}

%}
